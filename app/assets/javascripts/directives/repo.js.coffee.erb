App.directive 'repo', ['Subscription', (Subscription) ->
  Stripe.setPublishableKey('<%= ENV['STRIPE_PUBLISHABLE_KEY'] %>')

  scope: true
  templateUrl: '/templates/repo'
  link: (scope, element, attributes) ->
    repo = scope.repo
    scope.processing = false
    scope.paying = false
    scope.errorMessage = null

    activate = ->
      repo.$activate()
        .then(->
          scope.processing = false
          scope.paying = false
        ).catch(-> alert('Your repo failed to activate.'))

    deactivate = ->
      repo.$deactivate()
        .then(-> scope.processing = false)
        .catch(-> alert('Your repo failed to deactivate.'))

    createSubscription = (cardToken = null) ->
      subscription = new Subscription(
        repo_id: repo.id
        card_token: cardToken
      )
      subscription.$save().then(->
        scope.processing = false
        scope.paying = false
        repo.active = true
      , ->
        scope.processing = false
        scope.errorMessage = 'We encountered an error processing your payment. Please try a different card.'
      )

    scope.showPaymentForm = ->
      repo.private && scope.paying

    scope.toggle = ->
      scope.errorMessage = null

      if repo.active
        if repo.private
          scope.processing = true
          subscription = new Subscription(repo_id: repo.id)
          subscription.$delete().then(->
            repo.active = false
            scope.processing = false
          , ->
            alert('Your repo could not be disabled')
            scope.processing = false
          )
        else
          scope.processing = true
          deactivate(repo)
      else
        if repo.private
          scope.cardNumber = null
          scope.expirationMonth = null
          scope.expirationYear = null
          scope.cvc = null
          scope.paying = true
        else
          scope.processing = true
          activate(repo)

    scope.subscribeWithoutCard = ->
      event.preventDefault()
      scope.processing = true
      createSubscription()

    scope.subscribeWithCard = ->
      event.preventDefault()
      scope.processing = true

      Stripe.card.createToken(
        number: scope.cardNumber
        exp_month: scope.expirationMonth
        exp_year: scope.expirationYear
        cvc: scope.cvc
      , (status, response) ->
        if response.error
          scope.$apply ->
            scope.processing = false
            scope.errorMessage = 'We encountered an error processing your payment. Please verify your credit card details or try a different card.'
        else
          scope.$apply ->
            createSubscription(response.id)
      )
]
